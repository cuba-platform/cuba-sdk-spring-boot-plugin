
buildscript {
    ext.kotlin_version = '1.2.71'

    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

group 'com.haulmont.cuba.sdk.plugin.component.spring'
version '1.0-SNAPSHOT'

def moduleName = "com.haulmont.cuba.sdk.plugin.component.spring"

apply plugin: 'kotlin'
apply plugin: 'maven'

sourceCompatibility = 11
targetCompatibility = 11

repositories {
    jcenter()
//    mavenLocal()

    maven {
        url "https://dl.bintray.com/cuba-platform/main"
    }
}

task sourceJar(type: Jar) {
    from file('src/main/java')
    from file('src/main/kotlin')
    from file('src/main/resources')
    classifier = 'sources'
}

artifacts {
    archives sourceJar
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"

    implementation "com.haulmont.cli.core:cli-core:1.0.0"
    implementation "com.haulmont.cli.sdk:cuba-sdk:1.0.1"
    implementation "org.kodein.di:kodein-di-generic-jvm:5.0.0"

    implementation("com.google.guava:guava:28.2-jre") {
        exclude(group: 'com.google.code.findbugs', module: 'jsr305')
    }
    implementation("com.google.code.gson:gson:2.3.1")
}

[compileKotlin, compileTestKotlin].each {
    it.kotlinOptions.jvmTarget = '1.8'
}

compileJava {
    inputs.property("moduleName", moduleName)
    doFirst {
        options.compilerArgs = [
                '--module-path', classpath.asPath,
                '--patch-module', "$moduleName=${compileKotlin.destinationDir}"
        ]
        classpath = files()
    }
}

task installPlugin(group: 'install', dependsOn: jar, type: Copy) {
    inputs.files jar.outputs.files

    jar.outputs.files.each {
        from it
    }

    into System.getProperty("user.home") + "/.haulmont/sdk/plugins/"
}
